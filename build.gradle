plugins {
    id 'java'
    id "com.diffplug.spotless" version '6.8.0' apply false //루트에서 선언해줘야 하위 모듈에서 사용 가능
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar.enabled=false

subprojects { // 모든 하위 모듈들에 이 설정을 적용합니다.
    group 'com.whoz_in'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: "com.diffplug.spotless"

    ext {
        springBootVersion='3.3.5'
        springDependencyManagementVersion='1.1.5'
        springVersion='6.1.14'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // for DI
        compileOnly "org.springframework:spring-context:${springVersion}"

        //lombok
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'


        testImplementation 'com.tngtech.archunit:archunit-junit5:1.3.0'
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.mockito:mockito-core:5.2.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'
    }

    spotless {
        java {
            targetExclude 'build/' //빌드에 존재하는 건 제외
            removeUnusedImports() // 사용하지 않는 import 제거
            trimTrailingWhitespace() // 각 라인 끝에 있는 공백을 제거
            endWithNewline() // 파일 끝에 새로운 라인 추가
        }
    }

    tasks.named('compileJava') {
        dependsOn 'spotlessApply'
    }

    test {
        useJUnitPlatform()
    }
}